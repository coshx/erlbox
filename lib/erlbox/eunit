#!/usr/bin/env escript
%% Copyright (c) 2009 The Hive http://www.thehive.com/
%%
%% Permission is hereby granted, free of charge, to any person obtaining a copy
%% of this software and associated documentation files (the "Software"), to deal
%% in the Software without restriction, including without limitation the rights
%% to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
%% copies of the Software, and to permit persons to whom the Software is
%% furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in
%% all copies or substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
%% IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
%% FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
%% AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
%% LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
%% OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
%% THE SOFTWARE.
%%

main(Args) ->
    Opts = parse_args(Args, dict:new()),
    Files = find_test_files(Opts),
    ok = init_logging(Opts),
    maybe_cover_compile(Opts),
    eunit:test(test_names(Files), test_flags(Opts)),
    maybe_analyze_cover(Opts).


parse_args([], Opts) ->
    Opts;
parse_args(["-v"|Rest], Opts) ->
    parse_args(Rest, dict:store(verbose, true, Opts));
parse_args(["-cover"|Rest], Opts) ->
    parse_args(Rest, dict:store(cover, true, Opts));
parse_args(["-o", Dir|Rest], Opts) ->
    parse_args(Rest, dict:store(cover_dir, Dir, Opts));
parse_args(["-l", LogFile|Rest], Opts) ->
    parse_args(Rest, dict:store(log_file, LogFile, Opts));
parse_args(["-b", Path|Rest], Opts) ->
    true = code:add_path(Path),
    parse_args(Rest, dict:store(bin_dir, Path, Opts));
parse_args(["-s", SuiteName|Rest], Opts) ->
    Suite = list_to_atom(SuiteName),
    F = fun(Suites) -> [Suite|Suites] end,
    parse_args(Rest, dict:update(suites, F, [Suite], Opts));
parse_args([Dir|Rest], Opts) ->
    true = code:add_patha(Dir),
    parse_args(Rest, dict:store(test_dir, Dir, Opts)).

find_test_files(Opts) ->
    case dict:find(suites, Opts) of
        error ->
            case dict:find(test_dir, Opts) of
                error ->
                    io:fwrite("Error! Must provide test directory or suite names."),
                    [];

                {ok, Dir} ->
                    case file:list_dir(Dir) of
                        {ok, Files} ->
                            F = fun(File, Acc0) ->
                                    case lists:suffix("_tests.erl", File) of
                                        true  -> [File|Acc0];
                                        false -> Acc0
                                    end
                                end,

                            lists:foldl(F, [], Files);

                        _ ->
                            []
                    end
            end;

        {ok, Suites} ->
            Suites
    end.

init_logging(Opts) ->
    % Turn off logging to to tty
    ok = error_logger:tty(false),
    % send logging to a log file
    case dict:find(log_file, Opts) of
        error ->
            ok; % logging disabled
        {ok, LogFile} ->
            error_logger:logfile({open, LogFile})
    end.

test_names(Files) ->
    [list_to_atom(filename:basename(F, ".erl")) || F <- Files].

test_flags(Opts) ->
    case dict:find(verbose, Opts) of
        {ok, true}  -> [verbose];
        {ok, false} -> [];
        error       -> []
    end.

cover_enabled(Opts) ->
    case dict:find(cover, Opts) of
        {ok, true}  -> true;
        {ok, false} -> false;
        error       -> false
    end.

maybe_cover_compile(Opts) ->
    case cover_enabled(Opts) of
        true ->
            case dict:find(bin_dir, Opts) of
                error        -> BinDir = "./ebin";
                {ok, BinDir} -> BinDir
            end,
            io:fwrite("Cover compiling modules in ~s~n", [BinDir]),
            cover:compile_beam_directory(BinDir);

        false ->
            ok
    end.

maybe_analyze_cover(Opts) ->
    case cover_enabled(Opts) of
        true ->
            io:fwrite("Generating coverage report...~n"),
            case dict:find(cover_dir, Opts) of
                error          -> CoverDir = "./coverage";
                {ok, CoverDir} -> CoverDir
            end,
            ok = filelib:ensure_dir(filename:join(CoverDir, ".dummy")),
    		cover:export(filename:join(CoverDir, "all.coverdata")),

            Modules = lists:reverse(cover:modules()),
            F = fun(Mod, PctList) ->
                    {ok, {_, {Cov, NotCov}}} = cover:analyse(Mod, module),
                    Pct = (Cov / (Cov + NotCov)) * 100,
                    io:fwrite("~5B ~5B ~5.1f% ~p~n", [Cov, NotCov, Pct, Mod]),

                    OutFile = filename:join([CoverDir,
                        atom_to_list(Mod) ++ ".COVER.html"]),
                    cover:analyse_to_file(Mod, OutFile, [html]),

                    [Pct|PctList]
                end,
            PctList = lists:foldl(F, [], Modules),
            io:fwrite("~17.1f% TOTAL", [lists:sum(PctList) / length(PctList)]);

        false ->
            ok
    end.
